-- @nsURI TOSCA=http://tosca4cloudify/1.0
-- @nsURI DDSM=http://ddsm/1.0
-- @atlcompiler emftvm

module ddsm2tosca;
create tosca : TOSCA from ddsm : DDSM;

rule DDSM2TOSCA{	
	from 
		s: DDSM!DDSM
	using{
		flexiantImports: Sequence(TOSCA!Import) = thisModule.getFlexiantImport();
		additionalNodes: Sequence(TOSCA!NodeTemplate) = s.cloudelement
			-> select(element | element.oclIsTypeOf(DDSM!VM))
			-> select(vm | vm.publicPorts.size() > 0)
			-> collect(frontendVm | thisModule.getFloatingIpNodeFromFrontend(frontendVm.elementId, frontendVm.publicPorts)).flatten();
		zookeeperNodes: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!ZookeeperServer))
			-> collect(element | thisModule.getZookeeperServerNodeTemplate(element)).flatten();
		zookeeperQuorumNodes: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!ZookeeperQuorum))
			-> collect(element | thisModule.getZookeeperQuorumNodeTemplate(element)).flatten();
		zookeeperCluster: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!ZookeeperCluster))
			-> collect(element | thisModule.getZookeeperCluster(element)).flatten();
		nimbusNodes: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!Nimbus))
			-> collect(element | thisModule.getNimbusNodeTemplate(element)).flatten();
		supervisorNodes: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!Supervisor))
			-> collect(element | thisModule.getStormSupervisorNodeTemplate(element)).flatten();
		stormCluster: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!StormCluster))
			-> collect(element | thisModule.getStormCluster(element)).flatten();
		cassandraWorkers: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!CassandraWorker))
			-> collect(element | thisModule.getCassandraWorkerNodeTemplate(element)).flatten();
		cassandraSeeds: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!CassandraSeed))
			-> collect(element | thisModule.getCassandraSeedNodeTemplate(element)).flatten();
		cassandraClusters: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!CassandraCluster))
			-> collect(element | thisModule.getCassandraCluster(element)).flatten();
		storm_clients: Sequence(TOSCA!NodeTemplate) = s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!ClientNode))
			-> collect(element | thisModule.getStormClientNodeTemplate(element)).flatten();
	}
	to 
		t: TOSCA!TopologyTemplate(
			tosca_definitions_version <- 'cloudify_dsl_1_1',
			
			description <- s.description,
					
			nodeTemplates <- s.cloudelement 
			-> select(element | element.oclIsKindOf(DDSM!VM))
			-> collect(element | thisModule.getNodeTemplatesFromVM(element)).
			union(s.cloudelement 
			-> select(element | element.oclIsTypeOf(DDSM!InternalComponent))
			-> collect(element | thisModule.getNodeTemplatesFromInternalComponent(element))).
			union(zookeeperNodes).
			union(zookeeperQuorumNodes).
			union(nimbusNodes).
			union(supervisorNodes).
			union(stormCluster).
			union(zookeeperCluster).
			union(cassandraWorkers).
			union(cassandraSeeds).
			union(cassandraClusters).
			union(storm_clients)
		)
	do {
		for (p in DDSM!Provider.allInstances() ){
		
			if (p.isFlexiant()) {
				t.imports <- flexiantImports;
			}
		}	
				
	}
}


-------------------- Client rules --------------------
rule getStormClientNodeTemplate (s: DDSM!ClientNode) {
    using {
    	flexiantImports: Sequence(TOSCA!Import) = thisModule.getFlexiantImport();
		application: Sequence(TOSCA!Property) = thisModule.getPropertyFromStringAttribute('application', s.submits.artifactUrl);
		topology_name: Sequence(TOSCA!Property) = thisModule.getPropertyFromStringAttribute('topology_name', s.submits.elementId);
		topology_class: Sequence(TOSCA!Property) = thisModule.getPropertyFromStringAttribute('topology_class', s.submits.mainClass);
    	storm_client_properties: Sequence(TOSCA!Property) = Sequence{application, topology_name, topology_class}.flatten();
	}
	to
		t: TOSCA!NodeTemplate (
			node_template_name <- s.elementId,
			type <- 'dice.components.storm.Topology',
			 
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)).
				union(storm_client_properties),
				
			relationships <- s.requiredexecutionplatform -> select(platform |  DDSM!ExecutionBinding.allInstances() ->  exists(binding | 
			binding.requiredexecutionplatform.elementId = platform.elementId and binding.providedexecutionplatform.owner.oclIsTypeOf(DDSM!Nimbus)
			)) ->
			collect( platform | 
			thisModule.getStormClientRelationshipsFromExecutionBindingWithNimbus(platform).flatten()).union(
				
			s.requiredexecutionplatform -> collect(platform |  DDSM!ExecutionBinding.allInstances() ->  select(binding | 
			binding.requiredexecutionplatform.elementId = platform.elementId and binding.providedexecutionplatform.owner.oclIsTypeOf(DDSM!StormCluster)
			)).flatten() ->
			collect( binding | 
			thisModule.getStormClientRelationshipsFromExecutionBindingWithStormCluster(binding.providedexecutionplatform.owner).flatten())
			
			).flatten()
		)
	do {
		Sequence{t};
	}
}

rule getStormClientRelationshipsFromExecutionBindingWithStormCluster(s: DDSM!StormCluster) {
	using {
		supervisor_bindings: Sequence(DDSM!ExecutionBinding) = s.requiresSlaveVm ->  collect(platform | 
			DDSM!ExecutionBinding.allInstances() -> select(binding | binding.requiredexecutionplatform.elementId = platform.elementId)
		).flatten();
		client_to_storms: Sequence(TOSCA!Relationship) = supervisor_bindings -> collect(
			binding | thisModule.getClientToStormRelationship(s.elementId + '_' + binding.providedexecutionplatform.owner.elementId + '_supervisor')
		);
	}
	to
		client_to_nimbus: TOSCA!Relationship(
			target <- s.elementId + '_nimbus',
			type <- 'dice.relationships.storm.SubmittedBy'
		)
	do {
		Sequence{client_to_nimbus, client_to_storms};
	}
}

rule getStormClientRelationshipsFromExecutionBindingWithNimbus(s: DDSM!ExecutionBinding) {
	using {
		required_supervisors: Sequence(DDSM!Supervisor) = DDSM!Supervisor.allInstances() ->  any(supervisor | 
			supervisor.hasMaster.elementId = s.providedexecutionplatform.owner.elementId
		);
		client_to_storms: Sequence(TOSCA!Relationship) = required_supervisors -> collect(supervisor | thisModule.getClientToStormRelationship(supervisor.elementId));
	}
	to
		client_to_nimbus: TOSCA!Relationship(
			target <- s.providedexecutionplatform.owner.elementId,
			type <- 'dice.relationships.storm.SubmittedBy'
		)
	do {
		Sequence{client_to_nimbus, client_to_storms};
	}
}

lazy rule getClientToStormRelationship {
	from 
		s: String
	to 
		t: TOSCA!Relationship (
			target <- s,
			type <- 'dice.relationships.Needs'	
		)
}
-------------------- Client rules --------------------

-------------------- Zookeeper rules --------------------

rule getZookeeperCluster ( zookeeperCluster: DDSM!ZookeeperCluster) {
	using {
		peers_exec_bindings: Sequence(DDSM!ExecutionBinding) = zookeeperCluster.requiresPeerVm -> collect(peer_required_platform |  DDSM!ExecutionBinding.allInstances() -> 
			any(binding | binding.requiredexecutionplatform.elementId = peer_required_platform.elementId));
		peers: Sequence(TOSCA!NodeTemplate) = peers_exec_bindings -> collect(binding | thisModule.getPeerNodeTemplateForZookeeperCluster(binding.providedexecutionplatform.owner, zookeeperCluster));
	}
	to
		quorum: TOSCA!NodeTemplate (
			node_template_name <- zookeeperCluster.elementId + '_quorum',	
			type <- 'dice.components.zookeeper.Quorum', 
			
			relationships <- thisModule.getZookeeperQuorumContainsRelationshipForZookeeperCluster(zookeeperCluster).
			union(
				zookeeperCluster.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			))))
		)
	do {
		Sequence{peers, quorum};
	}
}

rule getPeerNodeTemplateForZookeeperCluster (host: DDSM!VM, zookeeperCluster: DDSM!ZookeeperCluster) {
	to
		zookeeper_peer: TOSCA!NodeTemplate(
			node_template_name <- zookeeperCluster.elementId + '_' + host.elementId + '_server',	
			type <- 'dice.components.zookeeper.Server', 
			
			relationships <- thisModule.getZookeeperServerRelationshipForZookeeperCluster(host, zookeeperCluster).
			union(
				zookeeperCluster.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			)))),
			
			properties <- thisModule.getPropertyFromAttribute('tickTime', 1500).union(
			thisModule.getPropertyFromAttribute('initLimit', 10)).union(
			thisModule.getPropertyFromAttribute('syncLimit', 5))
		)
	do {
		zookeeper_peer;
	}
}

rule getZookeeperServerRelationshipForZookeeperCluster(host: DDSM!VM, zookeeperCluster: DDSM!ZookeeperCluster) {
	to
		zookeeper_to_host: TOSCA!Relationship(
			target <- host.elementId,
			type <- 'dice.relationships.ContainedIn'
		),
		zookeeper_to_quorum: TOSCA!Relationship(
			target <- zookeeperCluster.elementId + '_quorum',
			type <- 'dice.relationships.zookeeper.MemberOfQuorum'
		)
	do {
		Sequence{zookeeper_to_host, zookeeper_to_quorum};
	}
}

rule getZookeeperQuorumContainsRelationshipForZookeeperCluster(zookeeperCluster: DDSM!ZookeeperCluster) {
	to
		t: TOSCA!Relationship (
			target <- (DDSM!ExecutionBinding.allInstances() -> 
			any(binding | binding.requiredexecutionplatform.elementId = zookeeperCluster.requiresQuorumVm.elementId)).providedexecutionplatform.owner.elementId,

			type <- 'dice.relationships.zookeeper.QuorumContains'
		)
	do {
		Sequence{t};
	}
}

rule getZookeeperServerNodeTemplate (s: DDSM!ZookeeperServer) {
	using {
		tickTime: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('tickTime', s.tickTime);
		initLimit: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('initLimit', s.initLimit);
		syncLimit: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('syncLimit', s.syncLimit);
		zookeeper_properties: Sequence(TOSCA!Property) = Sequence{tickTime, initLimit, syncLimit}.flatten();
		member_of_quorum_relationship: Sequence(TOSCA!Relationship) = thisModule.getZookeeperMemberOfQuorumRelationship(s);
	}
	to
		server: TOSCA!NodeTemplate (
			node_template_name <- s.elementId,	
			type <- 'dice.components.zookeeper.Server', 
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 		
			binding.requiredexecutionplatform.elementId = platform.elementId
			))).union(
				s.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			)))).union(
				member_of_quorum_relationship
			),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)).
			union(zookeeper_properties),
			
			interfaces <- 
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!Resource))
			-> collect(resource | thisModule.getInterfaceFromResource(resource)).union(
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!ChefResource))
			-> collect(chef_resource | thisModule.getInterfaceFromChefResource(chef_resource))
			)	
		)
	do {
		Sequence{server};
	}
}

rule getZookeeperQuorumNodeTemplate (s: DDSM!ZookeeperQuorum) {
	to
		quorum: TOSCA!NodeTemplate (
			node_template_name <- s.elementId,	
			type <- 'dice.components.zookeeper.Quorum', 
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getZookeeperQuorumContainsRelationship(DDSM!ExecutionBinding.allInstances() ->  any(binding | 
			binding.requiredexecutionplatform.elementId = platform.elementId
			)))
			
		)
	do {
		Sequence{quorum};
	}
}

lazy rule getZookeeperQuorumContainsRelationship {
	from
		s: DDSM!ExecutionBinding
	to
		t: TOSCA!Relationship (
			--relationship_name <- s.elementId + '_quorum',
			target <- s.providedexecutionplatform.owner.elementId,
			type <- 'dice.relationships.zookeeper.QuorumContains'
		)
}

rule getZookeeperMemberOfQuorumRelationship(s: DDSM!ZookeeperServer) {
	to
		t: TOSCA!Relationship (
			--relationship_name <- s.elementId + '_quorum',
			target <- s.belongsToQuorum.elementId,
			type <- 'dice.relationships.zookeeper.MemberOfQuorum'
		)
	do {
		Sequence{t};
	}
}

-------------------- Zookeeper rules --------------------

-------------------- Cassandra rules --------------------

rule getCassandraSeedNodeTemplate (s: DDSM!CassandraSeed){
	to 
		t: TOSCA!NodeTemplate(
			node_template_name <- s.elementId,
			type <- 'dice.components.cassandra.Seed',
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 		
			binding.requiredexecutionplatform.elementId = platform.elementId
			))),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property))
		)
	do {
		Sequence{t};
	}
}

rule getCassandraWorkerNodeTemplate (s: DDSM!CassandraWorker){
	using {
		connected_to_seed_relationship: Sequence(TOSCA!Relationship) = thisModule.getWorkerToSeedRelationship(s);
	}
	to
		t: TOSCA!NodeTemplate(
			node_template_name <- s.elementId,
			type <- 'dice.components.cassandra.Worker',
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 		
			binding.requiredexecutionplatform.elementId = platform.elementId
			))).union(
				connected_to_seed_relationship
			),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property))
		)
	do {
		Sequence{t};
	}
}

rule getWorkerToSeedRelationship (s: DDSM!CassandraWorker) {
	to
		t: TOSCA!Relationship (
			--relationship_name <- s.elementId + '_quorum',
			target <- s.belongsToQuorum.elementId,
			type <- 'dice.relationships.cassandra.ConnectedToSeed'
		)
	do {
		Sequence{t};
	}
}

rule getCassandraCluster (cassandraCluster: DDSM!CassandraCluster){
	using {
		workers_exec_bindings: Sequence(DDSM!ExecutionBinding) = cassandraCluster.requiresPeerVm -> collect(worker_required_platform |  DDSM!ExecutionBinding.allInstances() -> 
			any(binding | binding.requiredexecutionplatform.elementId = worker_required_platform.elementId));
		workers: Sequence(TOSCA!NodeTemplate) = workers_exec_bindings -> collect(binding | thisModule.getWorkerNodeTemplateForCassandraCluster(binding.providedexecutionplatform.owner, cassandraCluster));
	}
	to
		seed: TOSCA!NodeTemplate (
			node_template_name <- cassandraCluster.elementId + '_seed',	
			type <- 'dice.components.cassandra.Seed', 
			
			relationships <- thisModule.getCassandraContainedInRelationshipForCassandraCluster(cassandraCluster).
			union(
				cassandraCluster.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			))))
		)
	do {
		Sequence{seed, workers};
	}
}

rule getCassandraContainedInRelationshipForCassandraCluster(cassandraCluster: DDSM!CassandraCluster) {
	to
		t: TOSCA!Relationship (
			target <- (DDSM!ExecutionBinding.allInstances() -> 
				any(binding | binding.requiredexecutionplatform.elementId = cassandraCluster.requiresQuorumVm.elementId)).providedexecutionplatform.owner.elementId,
			
			type <- 'dice.relationships.ContainedIn'
		)
	do {
		Sequence{t};
	}
}

rule getWorkerNodeTemplateForCassandraCluster (host: DDSM!VM, cassandraCluster: DDSM!CassandraCluster) {
	to
		cassandra_worker: TOSCA!NodeTemplate(
			node_template_name <- cassandraCluster.elementId + '_' + host.elementId + '_worker',	
			type <- 'dice.components.cassandra.Worker', 
			
			relationships <- thisModule.getCassandraWorkerRelationshipForCassandraCluster(host, cassandraCluster).
			union(
				cassandraCluster.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			))))
		)
	do {
		cassandra_worker;
	}
}

rule getCassandraWorkerRelationshipForCassandraCluster(host: DDSM!VM, cassandraCluster: DDSM!CassandraCluster) {
	to
		worker_to_host: TOSCA!Relationship(
			target <- host.elementId,
			type <- 'dice.relationships.ContainedIn'
		),
		worker_to_seed: TOSCA!Relationship(
			target <- cassandraCluster.elementId + '_seed',
			type <- 'dice.relationships.cassandra.ConnectedToSeed'
		)
	do {
		Sequence{worker_to_host, worker_to_seed};
	}
}

-------------------- Cassandra rules --------------------

-------------------- Storm rules --------------------

rule getStormCluster (stormCluster: DDSM!StormCluster) {
	using {
		slaves_exec_bindings: Sequence(DDSM!ExecutionBinding) = stormCluster.requiresSlaveVm -> collect(slave_required_platform |  DDSM!ExecutionBinding.allInstances() -> 
			any(binding | binding.requiredexecutionplatform.elementId = slave_required_platform.elementId));
		slaves: Sequence(TOSCA!NodeTemplate) = slaves_exec_bindings -> collect(binding | thisModule.getSupervisorNodeTemplateForStormCluster(binding.providedexecutionplatform.owner, stormCluster));
	}
	to 
		nimbus: TOSCA!NodeTemplate (
			node_template_name <- stormCluster.elementId + '_nimbus',	
			type <- 'dice.components.storm.Nimbus', 
			relationships <- thisModule.getNimbusRelationshipForStormCluster(stormCluster).
			union(
				stormCluster.requiredport -> collect( port | 
				thisModule.getConnectedToZookeeperQuorumRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId and 
				(relationship.providedport.owner.oclIsTypeOf(DDSM!ZookeeperQuorum) or relationship.providedport.owner.oclIsTypeOf(DDSM!ZookeeperCluster))
			)))),
			
			properties <- thisModule.getPropertyFromAttribute('taskTimeout', 30).union(
			thisModule.getPropertyFromAttribute('supervisorTimeout', 60)).union(
			thisModule.getPropertyFromAttribute('monitorFrequency', 10)).union(
			thisModule.getPropertyFromAttribute('queueSize', 100000)).union(
			thisModule.getPropertyFromAttribute('retryTimes', 5)).union(
			thisModule.getPropertyFromAttribute('retryInterval', 2000))
		)		
	do {
		Sequence{nimbus, slaves};
	}
}

rule getSupervisorNodeTemplateForStormCluster(host: DDSM!VM, stormCluster: DDSM!StormCluster) {
	to
		supervisor: TOSCA!NodeTemplate(
			node_template_name <- stormCluster.elementId + '_' + host.elementId + '_supervisor',	
			type <- 'dice.components.storm.Worker', 
			
			relationships <- thisModule.getSupervisorRelationshipForStormCluster(host, stormCluster).
			union(
				stormCluster.requiredport -> collect( port | 
				thisModule.getConnectedToZookeeperQuorumRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId and 
				(relationship.providedport.owner.oclIsTypeOf(DDSM!ZookeeperQuorum) or relationship.providedport.owner.oclIsTypeOf(DDSM!ZookeeperCluster))
			)))),
			
			properties <- thisModule.getPropertyFromAttribute('workerStartTimeout', 120).union(
			thisModule.getPropertyFromAttribute('heartbeatFrequency', 5)).union(
			thisModule.getPropertyFromAttribute('cpuCapacity', 400)).union(
			thisModule.getPropertyFromAttribute('memoryCapacity', 4096))
		)
	do {
		supervisor;
	}
}

lazy rule getConnectedToZookeeperQuorumRelationship {
	from
		s:  DDSM!Relationship
	to
		t: TOSCA!Relationship(
			target <- s.providedport.owner.elementId,
			type <- 'dice.relationships.ConnectedToZookeeperQuorum'
		)
}

rule getSupervisorRelationshipForStormCluster(host: DDSM!VM, stormCluster: DDSM!StormCluster) {
	to
		supervisor_to_host: TOSCA!Relationship(
			target <- host.elementId,
			type <- 'dice.relationships.ContainedIn'
		),
		supervisor_to_nimbus: TOSCA!Relationship(
			target <- stormCluster.elementId + '_nimbus',
			type <- 'dice.relationships.ConnectedToNimbus'
		)
	do {
		Sequence{supervisor_to_host, supervisor_to_nimbus};
	}
}

rule getNimbusRelationshipForStormCluster(stormCluster: DDSM!StormCluster) {
	to
		nimbus_to_host: TOSCA!Relationship(
		    target <- (DDSM!ExecutionBinding.allInstances() -> 
				any(binding | binding.requiredexecutionplatform.elementId = stormCluster.requiresMasterVm.elementId)).providedexecutionplatform.owner.elementId,
			
			type <- 'dice.relationships.ContainedIn'
		)
	do {
		Sequence{nimbus_to_host};
	}
}

rule getNimbusNodeTemplate (s: DDSM!Nimbus) {
	using {
		taskTimeout: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('taskTimeout', s.taskTimeout);
		supervisorTimeout: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('supervisorTimeout', s.supervisorTimeout);
		monitorFrequency: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('monitorFrequency', s.monitorFrequency);
		queueSize: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('queueSize', s.queueSize);
		retryTimes: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('retryTimes', s.retryTimes);
		retryInterval: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('retryInterval', s.retryInterval);
		nimbus_properties: Sequence(TOSCA!Property) = Sequence{taskTimeout, supervisorTimeout, monitorFrequency, queueSize, retryTimes, retryInterval}.flatten();
	}
	to
		nimbus: TOSCA!NodeTemplate (
			node_template_name <- s.elementId,	
			type <- 'dice.components.storm.Nimbus', 
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 		
			binding.requiredexecutionplatform.elementId = platform.elementId
			))).union(
				s.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			)))),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)).
			union(nimbus_properties),
			
			interfaces <- 
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!Resource))
			-> collect(resource | thisModule.getInterfaceFromResource(resource)).union(
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!ChefResource))
			-> collect(chef_resource | thisModule.getInterfaceFromChefResource(chef_resource))
			)	
		)
	do {
		Sequence{nimbus};
	}
}

rule getStormSupervisorNodeTemplate (s: DDSM!Supervisor) {
	using {
		workerStartTimeout: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('workerStartTimeout', s.workerStartTimeout);
		heartbeatFrequency: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('heartbeatFrequency', s.heartbeatFrequency);
		cpuCapacity: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('cpuCapacity', s.cpuCapacity);
		memoryCapacity: Sequence(TOSCA!Property) = thisModule.getPropertyFromAttribute('memoryCapacity', s.memoryCapacity);
		supervisor_properties: Sequence(TOSCA!Property) = Sequence{workerStartTimeout, heartbeatFrequency, cpuCapacity, memoryCapacity}.flatten();
		has_master_relationship: Sequence(TOSCA!Relationship) = thisModule.getSupervisorHasMasterRelationship(s);	
	}
	to
		supervisor: TOSCA!NodeTemplate (
			node_template_name <- s.elementId,	
			type <- 'dice.components.storm.Worker', 
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 		
			binding.requiredexecutionplatform.elementId = platform.elementId
			))).union(
				s.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			)))).union(
				has_master_relationship
			),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)).
			union(supervisor_properties),
			
			interfaces <- 
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!Resource))
			-> collect(resource | thisModule.getInterfaceFromResource(resource)).union(
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!ChefResource))
			-> collect(chef_resource | thisModule.getInterfaceFromChefResource(chef_resource))
			)	
		)
	do {
		Sequence{supervisor};
	}
}

rule getSupervisorHasMasterRelationship(s: DDSM!Supervisor) {
	to
		t: TOSCA!Relationship (
			--relationship_name <- s.elementId + '_quorum',
			target <- s.hasMaster.elementId,
			type <- 'dice.relationships.storm.ConnectedToNimbus'
		)
	do {
		Sequence{t};
	}
}
-------------------- Storm rules --------------------

rule getFloatingIpNodeFromFrontend (elementId: String, publicPorts: Sequence(Integer)) {
	using {
		securityProperties: Sequence(TOSCA!Property) = publicPorts ->
		collect(port | thisModule.getPropertyFromAttribute('port', port)).flatten();
	}
	to
		t: TOSCA!NodeTemplate(
			node_template_name <- elementId + '_virtual_ip',
			type <- 'dice.virtual_ip'
		),
		u: TOSCA!NodeTemplate(
			node_template_name <- elementId + '_firewall',
			type <- 'dice.firewall_rules.' + elementId, 
			properties <- securityProperties
		)
	do {
		Sequence{t,u};
	}
		
}

rule getSecutiryHostRelationships (vmId: String) {
	to 
		ip: TOSCA!Relationship(
			--relationship_name <- vmId + '_to_virtual_ip',
			target <- vmId + '_virtual_ip',
			type <- 'dice.server_connected_to_virtual_ip'
		),
		sec: TOSCA!Relationship(
			--relationship_name <- vmId + '_to_firewall',
			target <- vmId + '_virtual_ip',
			type <- 'dice.server_connected_to_firewall'
		)
	do {
		Sequence{ip, sec};
	}
}

rule getNodeTemplatesFromVM(s: DDSM!VM){
	to
		t : TOSCA!NodeTemplate(
			node_template_name <- s.elementId,
            type <- s.getVMGenericSize.toLower(), 
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)),	
			
			instances <- thisModule.getInstancesElement(s.instances),
						
			interfaces <- 
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!Resource))
			-> collect(resource | thisModule.getInterfaceFromResource(resource)).union(
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!ChefResource))
			-> collect(chef_resource | thisModule.getInterfaceFromChefResource(chef_resource))
			)
		)
	do {
		if (s.publicPorts.size() > 0) {
			t.relationships <- thisModule.getSecutiryHostRelationships(s.elementId);
		}
		t;
	}
}

lazy rule getInstancesElement{
	from
		nInstances: Integer
	to
		instances_element: TOSCA!Instances(
			deploy <- nInstances	
		)
	do {
		instances_element;
	}
}

lazy rule getNodeTemplatesFromInternalComponent{
	from 
		s : DDSM!InternalComponent
	to
		t : TOSCA!NodeTemplate(
			node_template_name <- s.elementId,	
			type <- 'dice.' + s.oclType().refGetValue('name').toString().toLower(), 
			
			relationships <- s.requiredexecutionplatform -> collect( platform | 
			thisModule.getContainedInRelationshipFromExecutionBinding(DDSM!ExecutionBinding.allInstances() ->  any(binding | 
			binding.requiredexecutionplatform.elementId = platform.elementId
			))).union(
				s.requiredport -> collect( port | 
				thisModule.getConnectedToRelationshipFromRelationship(DDSM!Relationship.allInstances() ->  any(relationship | 
				relationship.requiredport.elementId = port.elementId
			)))),
			
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property)),
			
			interfaces <- 
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!Resource))
			-> collect(resource | thisModule.getInterfaceFromResource(resource)).union(
			s.resource 
			-> select(resource | resource.oclIsTypeOf(DDSM!ChefResource))
			-> collect(chef_resource | thisModule.getInterfaceFromChefResource(chef_resource))
			)	
		)
		
}

lazy rule getContainedInRelationshipFromExecutionBinding{
	from
		s:  DDSM!ExecutionBinding
	to
		t: TOSCA!Relationship(
			--relationship_name <- s.elementId,
			target <- s.providedexecutionplatform.owner.elementId,
			type <- 'dice.relationships.ContainedIn',
			source_interfaces <- s.requiredexecutionplatform.resource 
			-> collect(resource | thisModule.getInterfaceFromResource(resource)),
			target_interfaces <- s.providedexecutionplatform.resource 
			-> collect(resource | thisModule.getInterfaceFromResource(resource)),
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property))
		)
}

lazy rule getConnectedToRelationshipFromRelationship {
	from
		s:  DDSM!Relationship
	to
		t: TOSCA!Relationship(
			--relationship_name <- s.elementId,
			target <- s.providedport.owner.elementId,
			type <- 'dice.relationships.ConnectedTo',
			source_interfaces <- s.requiredport.resource 
			-> collect(resource | thisModule.getInterfaceFromResource(resource)),
			target_interfaces <- s.providedport.resource 
			-> collect(resource | thisModule.getInterfaceFromResource(resource)),
			properties <- s.property 
			-> collect(property | thisModule.getProperty(property))
		)
		
}

helper context DDSM!Provider def: isFlexiant(): Boolean =
	if self.type = #Flexiant then
		true
	else
		false
	endif
;

rule getFlexiantImport(){
	using {
		FCO_CLIENT_PLUGIN_VERSION: String = 'develop';
		PLATFORM: String = 'fco';
		FCO_PLUGIN_VERSION: String = 'master';
	}
	to 
		i1: TOSCA!Import(
			file <- 'http://www.getcloudify.org/spec/cloudify/3.3.1/types.yaml'
		),
		i2: TOSCA!Import(
			file <- 'https://raw.githubusercontent.com/dice-project/DICE-FCO-Plugin-Cloudify/' + FCO_CLIENT_PLUGIN_VERSION + '/plugin.yaml'
		),
		i3: TOSCA!Import(
			file <- 'http://www.getcloudify.org/spec/chef-plugin/1.3.1/plugin.yaml'
		),
		i4: TOSCA!Import(
			file <- 'http://dice-project.github.io/DICE-Deployment-Cloudify/spec/' + PLATFORM + '/' + FCO_PLUGIN_VERSION + '/plugin.yaml'
		)
	do {
		Sequence{i1,i2,i3,i4};	
	}
}

rule getOpenstackImport(){
	using {
		OPENSTACK_VERSION: String = '';
	}
	to 
		i1: TOSCA!Import(
			file <- 'openstack-example-import-1' + OPENSTACK_VERSION
		),
		i2: TOSCA!Import(
			file <- 'openstack-example-import-2' + OPENSTACK_VERSION
		)
	do {
		Sequence{i1,i2};	
	}
}

helper context DDSM!VM def: getVMGenericSize: String =
	if not self.genericSize.oclIsUndefined() then
		'dice.hosts.' + self.genericSize.toString().replaceAll('#', '')
	else
		'dice.hosts.GenericHost'
	endif;

lazy rule getProperty{
	from 
		s : DDSM!Property
	to
		t : TOSCA!Property(
			property_name <- s.propertyId,
			value <- s.value
		)
}

rule getPropertyFromAttribute(name: String, value: Integer){
	to
		t : TOSCA!Property(
			property_name <- name,
			value <- value.toString()
		)
	do {
		Sequence{t};
	}
}

rule getPropertyFromStringAttribute(name: String, value: String){
	to
		t : TOSCA!Property(
			property_name <- name,
			value <- value.toString()
		)
	do {
		Sequence{t};
	}
}
